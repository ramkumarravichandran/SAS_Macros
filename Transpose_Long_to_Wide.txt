/************************************************************************************************************
						Transposing - Long to Wide
			
Macro Objective: The macro will transpose the data from long format to wide format.Please see example below for better understanding
Prepared By:	 Rajat Mathur [Rajat.Mathur@DiamondConsultants.com]
Date of Creation: Jan 15th 2009
Edited By:
Edited On:	
Changes Made:

How to Use Macro:1. Include this file - "C:\xxx\Transpose_Long_to_Wide.txt";
		 2. Call the macro by putting appropriate inputs for each parameter
	
	Long_to_Wide(inp_long=,out_wide=,varlist=,unique_id=,suffix_var=);	 
	
		 Long_to_Wide(inp_long=<input_long_data>,out_wide=<output_wide_data>,
		 varlist=<list of variable that needs to be transposed>,
		 unique_id=<Variable(s) on which wide data is unique>,
		 suffix_var=<Name of variable that will act as a suffix in wide data set>
		
		 
		 where:
		 	data_in  = name of input data set. eg. libname.dataset
			data_out = name of output data set. eg. libname2.dataset2
			varlist  = list of ALL variables which needs to be transposed
			unique_id = Variable(s) on which wide data is unique 
			suffix_var  = Name of variable that will act as a suffix in wide data set
			
			
Illustrative Example:

Input Data Set:
famid  year 	faminc	spend 	debt

1 	96 	40000 	38000 	yes 
1 	97 	40500 	39000 	yes 
1 	98 	41000 	40000 	no 
2 	96 	45000 	42000 	yes 
2 	97 	45400 	43000 	no 
2 	98 	45800 	44000 	no 
3 	96 	75000 	70000 	no 
3 	97 	76000 	71000 	no 
3 	98 	77000 	72000 	no 


Output Data Set:
famid  faminc96  faminc97  faminc98  spend96  spend97  spend98  debt96  debt97  debt98

  1      40000     40500     41000    38000    39000    40000    yes     yes      no
  2      45000     45400     45800    42000    43000    44000    yes     no       no
  3      75000     76000     77000    70000    71000    72000    no      no       no 	

Here suffix_var is Year and value of year become suffix of variables in output dataset


Algo for Macro:
1. Sort the data on Unique ID(s)
2. For each variable use Proc Transpose to transpose the data and get output dataset for different variables
3. Merge all obtained data sets for each variable to get the output dataset

Reference: http://www.ats.ucla.edu/stat/sas/modules/ltow_transpose.htm

*************************************************************************************************************/
***************    		0. Start of SAS Macro                                      ******************;

%macro Long_to_Wide(inp_long=,out_wide=,varlist=,unique_id=,suffix_var=);
		%local temp_list;
		%let temp_list=;
		%local cnt this_var;
		%let this_var= %scan (&varlist, 1, %str(" "));
		%let cnt=1;

******************	1. Sort the data on Unique ID(s)		****************************************;
			
			proc sort data=&inp_long out=_long_;
			by &unique_id ;
			run;
			
		%do %while( &this_var ne );
		
************* 		2. For each variable use Proc Transpose to transpose the data and get *****************;
*************		output dataset for different variables		 		***********************;
			
			proc transpose data=_long_(keep=&this_var &unique_id &suffix_var) out=_&this_var._(drop=_name_) prefix=&this_var;
    			by &unique_id ;
    			id &suffix_var;
    			var &this_var;
			run;
			
			%let temp_list=&temp_list _&this_var._;

		%let cnt = %eval(&cnt+1);
		%let this_var = %scan(&varlist ,&cnt , %str(" "));
		%end;
	
**********	3. Merge all obtained data sets for each variable to get the output dataset	*****************;

		data &out_wide;
		merge &temp_list;
		by &unique_id;
		run;

%mend Long_to_Wide;
/********************* end of SAS macro*****************************************/




